plugins {
    id 'org.springframework.boot' version '2.5.0-SNAPSHOT'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.owasp.dependencycheck" version '6.1.5'
    id 'java'
    id "com.github.ben-manes.versions" version '0.38.0'
}

group = 'ch.aaap.ex1springreactive'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_16
    targetCompatibility = JavaVersion.VERSION_16
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://repo.spring.io/snapshot' }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    implementation 'org.projectlombok:lombok:1.18.20'
    implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.5.7'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'io.r2dbc:r2dbc-postgresql'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation 'org.testcontainers:r2dbc:1.15.3'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly "org.testcontainers:testcontainers:1.15.3"
    testRuntimeOnly "org.testcontainers:junit-jupiter:1.15.3"
    testRuntimeOnly 'org.testcontainers:postgresql:1.15.3'

    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}


task startPostgres(type: Exec) {
    commandLine './postgresql-start.sh'
}

bootRun.dependsOn startPostgres

dependencyCheck {
    suppressionFile './resources/cve-suppression.xml'
}

check.dependsOn dependencyCheckAnalyze
